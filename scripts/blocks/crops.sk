
on load:
    set {-features::croptrample::name} to "Crop trampling"
    set {-features::croptrample::desc} to "Crops cannot be trampled while wearing feather falling"
    set {-features::croptrample::icon} to farmland
    set {-features::planting::name} to "Planting"
    set {-features::planting::desc} to "Allows planting certain items onto blocks\nCurrently just seeds on dirt to make grass"
    set {-features::planting::icon} to wheat seeds

# no trample with feather falling
on block trample:
    {enabledFeatures::croptrample} is true
    if type of event-block is farmland:
        boots of event-entity are enchanted with feather falling
        cancel event

# apply seeds
on right click on dirt:
    {enabledFeatures::planting} is true
    player isn't sneaking
    player's tool is wheat seeds
    event-blockface is up_face
    cancel event
    set event-block to grass block
    play sound "minecraft:item.crop.plant" at event-block
    make player swing their hand
    player's gamemode isn't creative
    remove 1 of player's tool from player's tool
