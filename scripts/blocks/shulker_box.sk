
# keep components nbt when broken
on mine:
    event-block is tagged with (block tag "shulker_boxes")
    player's gamemode isn't creative
    (compound tag "components" of (full nbt of event-block)) is set
    set {_componentsnbt} to compound tag "components" of full nbt of event-block
    set {_drops::*} to (drops of event-block using player's tool)
    loop {_drops::*}:
        set {_item} to loop-value
        add {_componentsnbt} to nbt of {_item}
        set {_drops::%loop-iteration%} to {_item}
    size of {_drops::*} > 0
    clear drops
    drop {_drops::*} at location of event-block ~ vector(0.5,0.5,0.5)

import:
    io.papermc.paper.event.block.BlockBreakBlockEvent
on BlockBreakBlockEvent:
    event.getBlock() is tagged with (block tag "shulker_boxes")
    (compound tag "components" of (full nbt of event.getBlock())) is set
    set {_componentsnbt} to compound tag "components" of full nbt of event.getBlock()
    set {_drops::*} to ...event.getDrops()
    loop {_drops::*}:
        set {_item} to loop-value
        add {_componentsnbt} to nbt of {_item}
        set {_drops::%loop-iteration%} to {_item}
    size of {_drops::*} > 0
    event.getDrops().clear()
    drop {_drops::*} at location of event.getBlock() ~ vector(0.5,0.5,0.5)
