
on load:
    set {-features::loadstone::name} to "Loadstones"
    set {-features::loadstone::desc} to "Lodestones can be used as chunk-loaders\nUse an echo shard on a lodestone to activate it"
    set {-features::loadstone::warn} to "Disabling only disables creating new loadstones"
    set {-features::loadstone::icon} to lodestone

function deactivateLoadstone(block:block):
    {loadstones::*} contains {_block}
    remove {_block} from {loadstones::*}
    unload chunk at {_block}
    play sound "minecraft:block.beacon.deactivate" in blocks category at {_block}
    drop (1 of echo shard) at ({_block} ~ vector(0.5,0.5,0.5))

on load:
    set {_s} to "" if (size of {loadstones::*} is 1) else "s"
    set {_are} to "is" if (size of {loadstones::*} is 1) else "are"
    log info "There %{_are}% §b%size of {loadstones::*}% §rloadstone%{_s}% active."

every tick:
    loop {loadstones::*}:
        if loop-value isn't lodestone:
            deactivateLoadstone(loop-value)
            continue
        make 1 of sculk_charge using 0 at loop-value with offset vector(0.35,0.35,0.35) with extra 0.025
        load chunk at loop-value with ticket

on right click on lodestone:
    {enabledFeatures::loadstone} is true
    if event.getHand() is off hand slot:
        set {_tool} to player's offhand tool
    else:
        set {_tool} to player's tool
    (item id of {_tool}) is "minecraft:echo_shard"
    {loadstones::*} doesn't contain event-block
    if player's gamemode isn't creative:
        if event.getHand() is off hand slot:
            subtract 1 from (item amount of player's offhand tool)
        else:
            subtract 1 from (item amount of player's tool)
    add event-block to {loadstones::*}
    play sound "minecraft:block.beacon.activate" in blocks category at event-block
    grantAdvancement("turtle:nether/loadstone", player)
    cancel event
    make player swing their hand
    make 20 of sculk_charge using 0 at event-block with extra 0.1
