
when ready to register custom items:
    set (custom item "turtle:shulker_soul") to (quartz with nbt from "{""minecraft:item_model"":""turtle:shulker_soul"",""minecraft:item_name"":""Shulker Soul"",""minecraft:max_stack_size"":16}")
    loop "red","orange","yellow","lime","green","cyan","light_blue","blue","purple","magenta","pink","black","gray","light_gray","white","brown":
        set (custom item "turtle:shulker_shell_%loop-value%") to (shulker shell with nbt from "{""minecraft:item_model"":""turtle:shulker_shell_%loop-value%"",""minecraft:item_name"":""%proper case loop-value% Shulker Shell""}")

# packing
on right click:
    # shulker soul
    set {_e} to target entity of player
    set {_dist} to (distance between player's head and {_e})
    set {_attr} to (player entity interaction range final attribute of player)
    set {_e} to {_e} if ({_dist} < {_attr}) else {_}
    if (item id of item of {_e}) is "turtle:shulker_soul":
        (item amount of item of {_e}) is 1
        player's tool is shulker shell
        player's offhand tool is shulker shell
        (item id of player's tool) is (item id of player's offhand tool)
        cancel event
        kill entity within {_e}
        if player's gamemode isn't creative:
            remove (1 of player's tool) from (player's tool)
            remove (1 of player's offhand tool) from (player's offhand tool)
        set {_col} to (dye color of player's tool) ? ""
        spawn shulker at {_e}:
            set color of event-entity to {_col}
            play sound "minecraft:item.bottle.fill_dragonbreath" in player category at (event-entity's location)
            make 10 of poof at event-entity with extra 0.2
    # chest
    else if event-block is chest:
        player's tool is shulker shell
        player's offhand tool is shulker shell
        (item id of player's tool) is (item id of player's offhand tool)
        cancel event
        if player's gamemode isn't creative:
            remove (1 of player's tool) from (player's tool)
            remove (1 of player's offhand tool) from (player's offhand tool)
        set {_col} to (dye color str of (dye color of player's tool)) ? ""
        set {_shulker} to ("%{_col}% shulker box" parsed as itemtype)
        set block only of event-block to {_shulker}
        set blockdata tag "facing" of (block at event-block) to "up"
        play sound "minecraft:item.bottle.fill_dragonbreath" in player category at event-block
        make 10 of poof at event-block with extra 0.2
