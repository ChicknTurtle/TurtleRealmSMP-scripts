using reflection

options:
    maxAmount: 6400
    maxDistance: 30
    maxDistanceSneaking: 15

when ready to register custom items:
    # chorium case
    set (update function of "turtle:chorium_case") to (function "updateItem" in script)
    set (custom item "turtle:chorium_case") to (firework star with nbt from "{""minecraft:item_name"":""Chorium Case"",""minecraft:rarity"":""epic"",""minecraft:max_stack_size"":1}")
    apply use cooldown component to (custom item "turtle:chorium_case"):
        seconds: 1 second
        group: "minecraft:ender_pearl"

    set (update function of "turtle:chorium_case_filled") to (function "legacyChoriumCaseFilled" in script)
    set (custom item "turtle:chorium_case_filled") to (firework star)

local function legacyChoriumCaseFilled(item:item) :: item:
    set (item id of {_item}) to "turtle:chorium_case"
    return {_item}

when ready to load recipes:
    register smithing transform recipe:
        id: "turtle:chorium_case_smithing"
        result: custom item "turtle:chorium_case"
        template: custom item "turtle:chorium_upgrade_smithing_template"
        base: heart of the sea
        addition: custom item "turtle:chorium_ingot"

local function updateItem(item:item) :: item:
    set {_amount} to (int tag "storedPearls" of (custom nbt of {_item})) ? 0
    if {_amount} is 0:
        set (item model of {_item}) to "turtle:chorium_case"
        remove custom interact from {_item}
    else:
        set (item model of {_item}) to "turtle:chorium_case_filled"
        add custom interact to {_item}
    add "ยงd%{_amount}%ยง5/{@maxAmount}" to {_l::*}
    add "ยง7Capable of storing a large amount of ender pearls" to {_l::*}
    add "ยง7Use to teleport at the cost of a pearl" to {_l::*}
    set (lore of {_item}) to {_l::*}
    return {_item}

# show tp indicator
every 5 ticks:
    loop (all players where [(item id of input's tool) is "turtle:chorium_case"]):
        set {_amount} to (int tag "storedPearls" of (custom nbt of (loop-player's tool))) ? 0
        {_amount} > 0
        set {_dist} to ({@maxDistanceSneaking} if (loop-player is pressing sneak key) else {@maxDistance})
        set {_ray} to (raytrace from loop-player with max distance {_dist} while ignoring passable blocks)
        set {_hit} to (hit location of {_ray}) ? (location {_dist} blocks in front of loop-player's head)
        set {_portalhit::*} to ((blocks between (loop-player's head) and {_hit}) where [input is a nether portal])
        {_portalhit::*} isn't set
        make 1 of witch at {_hit} for loop-player

# teleport
on custom item interact with "turtle:chorium_case":
    set {_amount} to (int tag "storedPearls" of (custom nbt of event-item)) ? 0
    {_amount} > 0
    event-world isn't "world_turtle_dungeon"
    set {_case} to (player's offhand tool) if (event-equipmentslot is off hand slot) else (player's tool)
    set {_dist} to (({@maxDistanceSneaking} if (player is pressing sneak key) else {@maxDistance}))
    set {_ray} to (raytrace from player with max distance {_dist} while ignoring passable blocks)
    set {_hit} to (hit location of {_ray}) ? (location {_dist} blocks in front of player's head)
    set {_portalhit} to (first element of ((blocks between (player's head) and {_hit}) where [input is a nether portal]))
    set {_portalhit} to ({_portalhit} ~ vector(-0,-0.5,-0))
    set {_hit} to ({_portalhit} ? {_hit})
    set {_startLoc} to (player's location)
    set (yaw of {_hit}) to (yaw of player)
    set (pitch of {_hit}) to (pitch of player)
    make player dismount
    teleport player to {_hit} while retaining (vehicle,passengers,xyz velocity)
    set (fall distance of player) to 0
    set (boolean tag "ignore_fall_damage_from_current_explosion" of (full nbt of player)) to false
    set {_v} to normalized (vector from {_startLoc} to {_hit})
    set {_amount} to ceil(distance between {_startLoc} and {_hit})
    set {_amount} to max({_amount}, 1)
    set {_loc} to {_startLoc}
    loop {_amount} times:
        make 5 of portal at {_loc}
        make 5 of witch at {_loc}
        set {_loc} to ({_loc} ~ {_v})
    play sound "minecraft:entity.player.teleport" in player category at player
    # swing hand
    if event-equipmentslot is (off hand):
        make player swing their offhand
    else:
        make player swing their hand
    # lower count
    (player's gamemode) isn't creative
    set {_amount} to ((int tag "storedPearls" of custom nbt of {_case}) ? 1)
    set {_newamount} to {_amount} - 1
    set (int tag "storedPearls" of (custom nbt of {_case})) to {_newamount}
    set {_case} to updateItem({_case})
    if (event-equipmentslot is off hand slot):
        set (player's offhand tool) to {_case}
    else:
        set (player's tool) to {_case}

# insert/extract pearls
on inventory click:
    # case in inv
    if (item id of event-slot) is "turtle:chorium_case":
        (boolean tag "inFilter" of (custom nbt of event-slot)) isn't true
        # insert
        if (player's cursor slot) is ender pearl:
            event-clicktype is left mouse button
            cancel event
            # set vars
            set {_amount} to ((int tag "storedPearls" of custom nbt of event-slot) ? 0)
            set {_addamount} to (item amount of (player's cursor slot))
            set {_newamount} to {_amount} + {_addamount}
            if {_newamount} > {@maxAmount}:
                set {_remaining} to {_newamount} - {@maxAmount}
                set {_newamount} to {@maxAmount}
            else:
                set {_remaining} to 0
            # update item
            set (int tag "storedPearls" of (custom nbt of event-slot)) to {_newamount}
            set event-slot to updateItem(event-slot)
            set (player's cursor slot) to ({_remaining} of ender pearl)
            # effects
            play sound "minecraft:item.bundle.insert" in (players category) at player to player
        # extract
        else if (player's cursor slot) is air:
            event-clicktype is right mouse button
            cancel event
            # set vars
            set {_amount} to ((int tag "storedPearls" of custom nbt of event-slot) ? 0)
            set {_newamount} to {_amount} - 16
            if {_newamount} < 0:
                set {_take} to 16 + {_newamount}
                set {_newamount} to 0
            else:
                set {_take} to 16
            # update item
            set (int tag "storedPearls" of (custom nbt of event-slot)) to {_newamount}
            set event-slot to updateItem(event-slot)
            set (player's cursor slot) to ({_take} of ender pearl)
            # effects
            play sound "minecraft:item.bundle.remove_one" in (players category) at player to player
    # case in cursor
    else if (item id of (player's cursor slot)) is "turtle:chorium_case":
        (boolean tag "inFilter" of (custom nbt of event-slot)) isn't true
        # insert
        if event-slot is ender pearl:
            event-clicktype is left mouse button
            cancel event
            # set vars
            set {_amount} to ((int tag "storedPearls" of custom nbt of (player's cursor slot)) ? 0)
            set {_addamount} to (item amount of event-slot)
            set {_newamount} to {_amount} + {_addamount}
            if {_newamount} > {@maxAmount}:
                set {_remaining} to {_newamount} - {@maxAmount}
                set {_newamount} to {@maxAmount}
            else:
                set {_remaining} to 0
            # update item
            set (int tag "storedPearls" of (custom nbt of (player's cursor slot))) to {_newamount}
            set (player's cursor slot) to updateItem(player's cursor slot)
            set event-slot to ({_remaining} of ender pearl)
            # effects
            play sound "minecraft:item.bundle.insert" in (players category) at player to player
        # extract
        else if event-slot is air:
            event-clicktype is right mouse button
            cancel event
            # set vars
            set {_amount} to ((int tag "storedPearls" of custom nbt of (player's cursor slot)) ? 0)
            set {_newamount} to {_amount} - 16
            if {_newamount} < 0:
                set {_take} to 16 + {_newamount}
                set {_newamount} to 0
            else:
                set {_take} to 16
            # update item
            set (int tag "storedPearls" of (custom nbt of (player's cursor slot))) to {_newamount}
            set (player's cursor slot) to updateItem(player's cursor slot)
            set event-slot to ({_take} of ender pearl)
            # effects
            play sound "minecraft:item.bundle.remove_one" in (players category) at player to player

# collect pearls from ground
on entity added to world:
    event-entity is a dropped item
    (item id of item of event-entity) is "turtle:chorium_case"
    while chunk at event-entity is loaded:
        wait 1 tick
        if event-entity isn't alive:
            exit trigger
        # check pearls
        set {_pearls::*} to ((dropped items in radius 1.5 of event-entity) where [item of input is ender pearl])
        set {_amount} to (int tag "storedPearls" of (custom nbt of (item of event-entity)))
        loop (shuffled {_pearls::*}):
            set {_size} to (item amount of (item of loop-value))
            ({_amount} + {_size}) <= {@maxAmount}
            delete (entity within loop-value)
            set (int tag "storedPearls" of (custom nbt of (item of event-entity))) to ({_amount} + {_size})
            set (item of event-entity) to updateItem(item of event-entity)
            play sound "minecraft:item.bundle.insert" in players category at event-entity
            exit loop
