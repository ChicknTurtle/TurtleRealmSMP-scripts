
when ready to register custom items:
    set (custom item "turtle:chorium_elytra") to (elytra with nbt from "{""minecraft:item_model"":""turtle:chorium_elytra"",""minecraft:item_name"":""Chorium Plated Elytra"",""minecraft:rarity"":""epic"",""minecraft:max_damage"":600,""minecraft:enchantable"":{value:3}}")
    set repairable items of (custom item "turtle:chorium_elytra") to (custom item "turtle:chorium_ingot")
    apply equippable component to (custom item "turtle:chorium_elytra"):
        slot: chest slot
        equip_sound: "minecraft:item.armor.equip_diamond"
        asset_id: "turtle:chorium_elytra"
        damage_on_hurt: false

when ready to load recipes:
    register smithing transform recipe:
        id: "turtle:chorium_elytra_smithing"
        result: custom item "turtle:chorium_elytra"
        template: custom item "turtle:chorium_upgrade_smithing_template"
        base: elytra
        addition: custom item "turtle:chorium_ingot"

on toggle gliding:
    wait 1 tick
    while player is gliding:
        wait 1 tick
        (item id of (player's chestplate)) is "turtle:chorium_elytra"
        player is pressing sneak key
        apply slow falling without particles without the icon to (player) for (5 ticks) replacing existing effect
        chance of 0.25
        make 1 of poof at player with extra 0.1

on press of jump key:
    player is gliding
    player isn't pressing sneak key
    y of (velocity of player) <= 0
    (item id of (player's chestplate)) is "turtle:chorium_elytra"
    if player's gamemode isn't creative:
        loop 6 times:
            (durability of player's chestplate) > 1
            player.damageItemStack(chest slot, 1)
    make 1 explosion at player
    loop 10 times:
        push player up with speed 0.1
        make 1 witch at player with extra 0.25
        wait 1 tick
