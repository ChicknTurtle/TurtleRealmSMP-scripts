
when ready to register custom items:
    set (custom item "turtle:magnifying_glass") to (firework star with nbt from "{""minecraft:item_model"":""turtle:magnifying_glass"",""minecraft:item_name"":""Magnifying Glass"",""minecraft:max_stack_size"":1}")
    apply use cooldown component to (custom item "turtle:magnifying_glass"):
        seconds: 3 seconds
        group: "turtle:magnifying_glass"
    # lore
    set {_c::1} to (text component from "Â§7Use ")
    set {_c::2} to (keybind component from "key.use")
    set (color format of {_c::2}) to rgb(102,217,217)
    set {_c::3} to (text component from "Â§7 to display info about blocks and entities")
    set (italic format of {_c::*}) to false
    set component lore of (custom item "turtle:magnifying_glass") to (merge components {_c::*})

when ready to load recipes:
    register shaped recipe:
        id: "turtle:magnifying_glass"
        result: custom item "turtle:magnifying_glass"
        shape: "iii", "igi", " s "
        category: "equipment"
        ingredients:
            set ingredient of "i" to iron ingot
            set ingredient of "g" to material choice of (tag contents of item tag "glass")
            set ingredient of "s" to stick

import:
    com.destroystokyo.paper.entity.villager.ReputationType

on right click:
    (item id of player's tool) is "turtle:magnifying_glass"
    player.hasCooldown((custom item "turtle:magnifying_glass")) is false
    set {_e} to (clicked entity)
    set {_b} to (clicked block)
    if {_e} is set:
        # name
        set {_name} to (effective entity name of {_e})
        add "Â§7ðŸ”Ž Â§f%{_name}%" to {_info::*}
        # health
        if {_e} is a living entity:
            set {_health} to round({_e}.getHealth())
            set {_max_health} to round({_e}.getMaxHealth())
            add "   Â§câ¤ Â§7%{_health}%/%{_max_health}%" to {_info::*}
        # owner
        if owner of {_e} is set:
            set {_owner} to (owner of {_e})
            set {_owner_component} to playerComponent({_owner}) if {_owner} is a player else (name of {_owner})
            add merge components (text component from "   Â§6ðŸ‘‘ Â§7Owner: ") and {_owner_component} to {_info::*}
        # villagers
        if ({_e} is a villager):
            set {_villagerLevel} to (villager level of {_e})
            if {_villagerLevel} is set:
                add (merge components ("   Â§aðŸ‘¤ Â§7Level: ",(translate component from "merchant.level.%{_villagerLevel}%"))) to {_info::*}
            set {_reputations} to (try event-entity.getReputations())
            if {_reputations} is set:
                set {_cured} to whether ((...{_reputations}.values() where [input.getReputation(ReputationType.MAJOR_POSITIVE) >= 20]) is set)
                set {_cured} to "Yes" if ({_cured} is true) else "No"
                add "   Â§eðŸŽ Â§7Cured: Â§f%{_cured}%" to {_info::*}
            set {_workstation} to (work station of {_e})
            if {_workstation} is set:
                set {_workstationName} to (translate component from {_workstation})
                set {_x} to floor(x-coord of {_workstation})
                set {_y} to floor(y-coord of {_workstation})
                set {_z} to floor(z-coord of {_workstation})
                set {_workstationPos} to (text component from "Â§3%{_x}%Â§7,Â§3%{_y}%Â§7,Â§3%{_z}%")
                set (click event of {_workstationPos}) to (click event to run command "/turtle:lookat %{_x}% %{_y}% %{_z}%")
                add (merge components ("   Â§7ðŸ  Â§fWorkstation: ",{_workstationName}," at ",{_workstationPos})) to {_info::*}
        # breeding cooldown
        if {_e} is breedable:
            set {_age} to (age of {_e})
            if {_age} >= 0:
                set {_breedingCooldown} to ("Â§aNone" if {_age} <= 0 else formatTimespan({_age}*1 tick))
                add "   Â§dðŸ’• Â§7Breeding Cooldown: Â§f%{_breedingCooldown}%" to {_info::*}
            else:
                add "   Â§dðŸ¤ Â§7Grows up in: Â§f%formatTimespan(({_age}*-1)*1 tick)%" to {_info::*}
        # wandering trader despawn
        set {_despawnDelay} to (try {_e}.getDespawnDelay())
        if {_despawnDelay} is set:
            set {_despawnDelay} to formatTimespan({_despawnDelay}*1 tick)
            add "   Â§3âŒ› Â§7Despawns in: Â§f%{_despawnDelay}%" to {_info::*}
        # pandas
        set {_mainGene} to (main gene of {_e})
        if {_mainGene} is set:
            set {_mainGene} to (strict proper case "%{_mainGene}%")
            add "   Â§fðŸ¼ Â§7Main Gene: Â§f%{_mainGene}%" to {_info::*}
        set {_hiddenGene} to (hidden gene of {_e})
        if {_hiddenGene} is set:
            set {_hiddenGene} to (strict proper case "%{_hiddenGene}%")
            add "   Â§fðŸ¼ Â§7Hidden Gene: Â§f%{_hiddenGene}%" to {_info::*}
        # VIBING
        if ({_e} is dancing):
            add "   Â§dðŸŽ¶ Â§cVÂ§6IÂ§eBÂ§aIÂ§bNÂ§dG" to {_info::*}
        if (size of {_info::*}) <= 1:
            exit trigger
        send components {_info::*} to player
    else if {_b} is set:
        # name
        set {_name} to (translate component of {_b})
        set {_customName} to (name of block within {_b})
        if {_customName} is set:
            add (merge components ("Â§7ðŸ”Ž Â§f%{_customName}% (",{_name},")")) to {_info::*}
        else:
            add (merge components ("Â§7ðŸ”Ž ",{_name})) to {_info::*}
        # hopper filter
        if {_b} is a hopper:
            set {_filter} to (compound tag "filter" of (custom nbt of {_b}))
            set {_blacklist} to ((boolean tag "blacklist" of {_filter}) ? false)
            set {_ids::*} to (string list tag "ids" of {_filter})
            if all:
                {_filter} is set
                {_blacklist} is set
                {_ids::*} is set
            then:
                loop sorted {_ids::*}:
                    add (effective name of (custom item loop-value)) to {_names::*}
                set {_joinNames} to (join {_names::*} with "Â§f, ")
                if {_blacklist} is true:
                    add "   Â§3ðŸ”» Â§7Blacklist Filter: Â§f%{_joinNames}%" to {_info::*}
                else:
                    add "   Â§3ðŸ”» Â§7Whitelist Filter: Â§f%{_joinNames}%" to {_info::*}
            else:
                add "   Â§3ðŸ”» Â§7No Filter" to {_info::*}
        # jukebox
        if {_b} is a jukebox:
            set {_record} to (blockstate of {_b}).getRecord()
            if ({_record} ? air) isn't air:
                if (item id of {_record}) is "turtle:custom_music_disc":
                    if (name of {_record}) is set:
                        add "   Â§6ðŸŽµ Â§7Playing Custom Song: Â§b%(name of {_record})%" to {_record::*}
                    else:
                        add "   Â§6ðŸŽµ Â§7Playing Custom Song: Â§cinvalid" to {_record::*}
                else:
                    set {_song} to (namespaced key from (string tag "minecraft:jukebox_playable" of vanilla nbt of {_record})).value()
                    replace "_" with " " in {_song}
                    set {_song} to proper case {_song}
                    add "   Â§6ðŸŽµ Â§7Playing Â§a%{_song}%" to {_record::*}
            else:
                add "   Â§6ðŸŽµ Â§7Empty" to {_record::*}
            add (merge components {_record::*}) to {_info::*}
        # trial spawner
        if {_b} is a trial spawner:
            set {_cooldown} to (blockstate of {_b}).getCooldownEnd()
            if {_cooldown} is 0:
                set {_cooldownText} to "Â§fNone"
            else:
                set {_cooldownText} to "Â§f%formatTimespan(({_cooldown} - (game ticks of world of {_b})) * (1 tick))%"
            add "   Â§6âŒ› Â§7Cooldown: Â§f%{_cooldownText}%" to {_info::*}
        
        if (size of {_info::*}) <= 1:
            exit trigger
        send components {_info::*} to player
    else:
        exit trigger
    cancel event
    play sound "minecraft:ui.loom.take_result" in player category at player to player
    player.setCooldown((custom item "turtle:magnifying_glass"), floor(20*(float tag "minecraft:use_cooldown;seconds" of nbt of (custom item "turtle:magnifying_glass"))))
    make player swing their hand
