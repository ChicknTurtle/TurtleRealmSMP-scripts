
# EXAMPLE BOT DEFINITION
# My bot is defined in env.sk to hide the token
# 
# define new bot named "ChatLink": 
#     token: "<INSERT TOKEN HERE>"
#     intents: default intents
#     on ready:
#         log info "ðŸ¤– Discord chat link initialized"

on load:
    set {-features::discordlink::name} to "Discord link"
    set {-features::discordlink::desc} to "Links in-game chat with a discord channel"
    set {-features::discordlink::warn} to "If formatting of chat, join/leave, or death messages is disabled,\nthose messages won't show up on discord"
    set {-features::discordlink::icon} to purple dye

function discordSend(msg:string):
    {enabledFeatures::discordlink} is true
    post {_msg} to text channel with id {-env::discordLinkChannel}

function discordChat(msg:string, p:player):
    # waypoint
    if {_msg} contains "xaero-waypoint:":
        set {_parts::*} to {_msg} split at ":"
        loop {_parts::*}:
            loop-value is "xaero-waypoint"
            set {_name} to {_parts::%loop-iteration+1%}
            stop loop
        set {_name} to "Shared Waypoint" if ({_name} ? "") is ""
        discordSend(":round_pushpin: %{_p}'s name% shared a waypoint called **%{_name}%**")
        stop
    # chat
    set {_msg} to mcToMarkdown({_msg})
    discordSend("%{_p}'s name%: %{_msg}%")

on message received:
    {enabledFeatures::discordlink} is true
    event-user isn't a discord bot
    discord id of event-channel is {-env::discordLinkChannel}
    set {_c::1} to (text component from "%effective name of event-member%:")
    set (color format of {_c::1}) to (rgb(88, 101, 242))
    set (hover event of {_c::1}) to (hover event showing "Â§f%event-user%%nl%Â§7@%discord name of event-user%")
    set {_c::2} to " &f%event-message%"
    send component (merge components {_c::*}) to (all players and console)
