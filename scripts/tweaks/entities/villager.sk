
# forget far away job-sites
on entity start pathfinding:
    event-entity is a villager
    set {_job} to event-entity's work station
    if any:
        world of event-entity isn't world of {_job}
        distance between event-entity and {_job} > 256
    then:
        delete compound tag "Brain;memories;minecraft:job_site" of full nbt of target entity

import:
    java.lang.Integer as javaInteger

on load:
    set {-features::infinitetrading::name} to "Infinite trading"
    set {-features::infinitetrading::desc} to "Villager trades have infinite stock and no raising demand"
    set {-features::infinitetrading::warn} to "When disabled, previously interacted with villagers will still have infinite trades"
    set {-features::infinitetrading::icon} to emerald
    set {-features::rerolltrades::name} to "Reroll trades"
    set {-features::rerolltrades::desc} to "Use glowstone dust to reroll trades of a villager"
    set {-features::rerolltrades::icon} to glowstone dust

on right click on villager:
    set {_trades::*} to merchant recipes of event-entity
    # infinite trading
    if {enabledFeatures::infinitetrading} is true:
        loop {_trades::*}:
            loop-value.setMaxUses(javaInteger.MAX_VALUE)
            loop-value.setUses(0)
            loop-value.setDemand(0)
    # reroll trades
    if {enabledFeatures::rerolltrades} is true:
        size of {_trades::*} > 0
        getId(player's tool) is "minecraft:glowstone_dust"
        if event-entity.getVillagerExperience() is 0:
            cancel event
            event-entity.resetOffers()
            make event-entity play sound "entity.villager.trade"
            play sound "block.enchantment_table.use" with volume 0.2 and pitch 2
            make 15 of enchant at (event-entity's head)
            make player swing their hand
            player's gamemode isn't creative
            remove 1 of player's tool from player's tool
        else:
            cancel event
            event-entity.shakeHead()
