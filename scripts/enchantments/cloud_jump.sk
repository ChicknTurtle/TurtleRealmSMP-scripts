
#import:
#    io.papermc.paper.event.entity.EntityAttemptSmashAttackEvent

function cloudJumpBoost(p:player):
    # boost
    if {_p} is gliding:
        if {_p} is sneaking:
            exit trigger
        # tailwind
        push {_p} forwards with force 0.4 + 2*0.15
        push {_p} up with force 0.4 + 2*0.15
    else:
        # cloud jump
        set {_x} to 0
        set {_z} to 0
        set {_y} to 0.575
        add 0.1 * (level of jump boost of {_p}) to {_y}
        if ({_p}'s gamemode) isn't creative:
            add 0.1 to exhaustion of {_p}
        if {_p} is sprinting:
            add 0-sin(yaw of {_p})*0.55*(1+0.15*(level of speed of {_p})) to {_x}
            add cos(yaw of {_p})*0.55*(1+0.15*(level of speed of {_p})) to {_z}
            add 0.115 to {_y}
        set {_y} to max({_y}, y of {_p}'s velocity)
        add vector({_x},0,{_z}) to ({_p}'s velocity)
        set y of ({_p}'s velocity) to {_y}
    # fall damage
    set boolean tag "ignore_fall_damage_from_current_explosion" of full nbt of {_p} to true
    set (double list tag "current_explosion_impact_pos" of nbt of {_p}) to (x-coord of {_p}),(y-coord of {_p}),(z-coord of {_p})
    # effects
    play sound "minecraft:entity.wind_charge.wind_burst" in players category with pitch 0.75 at {_p}
    make 1 of gust emitter small at {_p}

on join:
    while player isn't on ground:
        wait 1 tick
        if player isn't online:
            exit loop
    set {-playerData::%player%::canCloudJump} to true

on player press of jump key:
    player isn't climbing
    player isn't riding
    player isn't in water
    player isn't in lava
    player isn't in bubble column
    player isn't flying
    {-playerData::%player%::lastOnGround} >= 1
    set {_e} to enchantment from key "turtle:cloud_jump"
    player's leggings are enchanted with {_e}
    {-playerData::%player%::canCloudJump} isn't false
    cloudJumpBoost(player)
    set {-playerData::%player%::canCloudJump} to false
    wait 1 tick
    if player is on ground:
        wait 1 tick
    while player isn't on ground:
        wait 1 tick
        if player isn't online:
            exit loop
    set {-playerData::%player%::canCloudJump} to true

on jump:
    set {-playerData::%player%::canCloudJump} to true
#on riptide:
#    set {-playerData::%player%::canCloudJump} to true
#on EntityAttemptSmashAttackEvent:
#    event.getEntity() is a player
#    event.getOriginalResult() is true
#    set {-playerData::%event.getEntity()%::canCloudJump} to true
