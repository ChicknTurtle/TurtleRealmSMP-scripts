
# Grant advancements
function grantAdvancement(adv:objects, p:objects):
    {_adv::*} is set
    {_p::*} is set
    loop {_adv::*}:
        set {_adv} to loop-value-1
        if ({_adv} isn't an advancement):
            set {_adv} to ({_adv} parsed as advancement)
        {_adv} is set
        loop {_p::*} where [input is a player]:
            set {_prog} to (advancement progress of {_adv} for loop-value-2)
            loop criteria of {_adv}:
                award criteria loop-value-3 of {_prog}

# Revoke advancements
function revokeAdvancement(adv:objects, p:objects):
    {_adv::*} is set
    {_p::*} is set
    loop {_adv::*}:
        set {_adv} to loop-value-1
        if ({_adv} isn't an advancement):
            set {_adv} to ({_adv} parsed as advancement)
        {_adv} is set
        loop {_p::*} where [input is a player]:
            set {_prog} to (advancement progress of {_adv} for loop-value-2)
            loop criteria of {_adv}:
                revoke criteria loop-value-3 of {_prog}

# Porcupine
on projectile collide:
    (arrows stuck in player) >= 15
    grantAdvancement("turtle:adventure/porcupine", event-entity)

# Big Trouble
on warden move:
    grantAdvancement("turtle:adventure/encounter_warden", (players in radius 32 of event-entity))
on spawn of warden:
    grantAdvancement("turtle:adventure/encounter_warden", (players in radius 32 of event-entity))

# Beast Slayer
on death of warden:
    grantAdvancement("turtle:adventure/kill_warden", attacker)

# Wandering Ripoff
on death of wandering trader:
    grantAdvancement("turtle:adventure/kill_wandering_trader", attacker)

# Under the Sea
every 5 seconds:
    set {_p::*} to (all players)
    filter {_p::*} to match all:
        input is in water
        (biome at input) is (warm ocean)
        (chunk at input) has blockdata (tube_coral_block[], brain_coral_block[], bubble_coral_block[], fire_coral_block[], or horn_coral_block[])
    grantAdvancement("turtle:adventure/under_the_sea", {_p::*})

# On a Rail
import:
    org.bukkit.event.vehicle.VehicleMoveEvent
on vehicle enter:
    event-entity is a player
    event.getVehicle() is (a regular minecart)
    set (metadata value "cartStart" of player) to (player's location)
on vehicle exit:
    event-entity is a player
    event.getVehicle() is (a regular minecart)
    delete (metadata value "cartStart" of player)
on VehicleMoveEvent:
    set {_cart} to event.getVehicle()
    set {_p} to (first element of ({_cart}'s passengers))
    metadata value "cartStart" of {_p} is set
    distance between (metadata value "cartStart" of {_p}) and ({_p}'s location) > 1000
    grantAdvancement("turtle:adventure/on_a_rail", {_p})

import:
    net.minecraft.world.damagesource.FallLocation
# Tarzan
on damage of player:
    set {_fallLoc} to FallLocation.getCurrentFallLocation(victim.getHandle())
    set {_id} to {_fallLoc}.id
    "%{_id}%" is "vines"
    (biome at victim) is ("jungle" parsed as biome, sparse jungle, or bamboo jungle)
    grantAdvancement("turtle:adventure/tarzan", victim)

# Feeling Blue
on consume of nether wart:
    wait 1 tick
    player has potion nausea
    set {_nauseas::*} to ((potion effects of player) where [type of input is nausea])
    set {_nauseas::*} to {_nauseas::*} where [seconds of (potion duration of input) is 15]
    (size of {_nauseas::*}) > 0
    grantAdvancement("turtle:nether/feeling_blue", player)
