
on load:
    set {-features::joinleavemsgs::name} to "Join/leave messages"
    set {-features::joinleavemsgs::desc} to "Better looking join and leave messages"
    set {-features::joinleavemsgs::icon} to oak door

    set {-features::deathmsgs::name} to "Death messages"
    set {-features::deathmsgs::desc} to "Better looking death messages"
    set {-features::deathmsgs::warn} to "Doesn't work for pets"
    set {-features::deathmsgs::icon} to skeleton skull

    set {-features::customchat::name} to "Custom chat"
    set {-features::customchat::desc} to "Better looking and colorable chat messages\n[item] is replaced with your held item"
    set {-features::customchat::icon} to oak sign

on join:
    {enabledFeatures::joinleavemsgs} is true
    set {_c::1} to "§a+ §7| "
    set {_c::2} to playerComponent(player)
    set {_c::3} to " joined the game"
    clear join message
    send components (merge components {_c::*}) to (all players and console)
    discordSend("**+** %player%")

on leave:
    {enabledFeatures::joinleavemsgs} is true
    set {_c::1} to "§c- §7| "
    set {_c::2} to playerComponent(player)
    set {_c::3} to " left the game"
    clear leave message
    send components (merge components {_c::*}) to (all players and console)
    discordSend("**-** %player%")

on death of player:
    event.getShowDeathMessages() is true
    {enabledFeatures::deathmsgs} is true
    discordSend(":skull: %uncolored death message%")
    set {_c} to (text component from "§c☠ ")
    set {_c1} to component death message
    set color format of {_c1} to light gray
    add {_c1} to {_c}
    set component death message to {_c}

on async chat:
    {enabledFeatures::customchat} is true
    set {_msg} to async chat message
    # create format
    # {_msg} is used for discord
    set {_p} to player
    set {_c::1} to playerComponent({_p})
    set color format of {_c::1} to light gray
    set {_c::2} to text component from "§7: &r"
    set {_c::3} to colored async chat message
    set click event of {_c::1} to click event to suggest command "/msg %name of {_p}% "
    set {_format} to merge components {_c::*}

    # replace [item]
    if "%{_format}%" contains "[item]":
        (player's tool) isn't air
        set {_name} to (effective name of (player's tool))
        component replace "\[item]" with (item chat component of (player's tool)) in {_format}
        replace "[item]" with "**[%{_name}%]**" in {_msg}

    # send message
    set async chat format to {_format}
    discordChat({_msg}, player)
