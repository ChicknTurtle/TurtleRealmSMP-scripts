
on load:
    set {-features::protecteditems::name} to "Protected items"
    set {-features::protecteditems::desc} to "Certain items are invulnerable, glow, and will never despawn\nSee 'Protect death loot' to disable only for players"
    set {-features::protecteditems::warn} to "When disabled, previously protected items will stay protected"
    set {-features::protecteditems::icon} to glowstone dust

function protectItemStack(item:item) :: item:
    {enabledFeatures::protecteditems} is true
    set (boolean tag "protectedItem" of (custom nbt of {_item})) to true
    return {_item}

function protectItem(e:entity):
    {enabledFeatures::protecteditems} is true
    {_e} is a dropped item
    set (boolean tag "custom;protectedItem" of (full nbt of {_e})) to true
    set glowing of {_e} to true

function isProtectedItem(e:entity) :: boolean:
    if all:
        {_e} is a dropped item
        (boolean tag "custom;protectedItem" of (full nbt of {_e})) is true
    then:
        return true
    else:
        return false

# convert protected itemstacks to protected items
on spawn of dropped item:
    (boolean tag "Item;components;minecraft:custom_data;protectedItem" of (nbt of event-entity)) is true
    delete (boolean tag "Item;components;minecraft:custom_data;protectedItem" of (nbt of event-entity))
    protectItem(event-entity)

# handle protected items

on damage of dropped item:
    isProtectedItem(victim) is true
    cancel event

on item despawn:
    isProtectedItem(event-entity) is true
    cancel event

on item merge:
    if any:
        isProtectedItem(event-entity) is true
        isProtectedItem(future event-entity) is true
    then:
        cancel event

on entity pickup:
    event-player isn't a player
    isProtectedItem(event-dropped item) is true
    cancel event
