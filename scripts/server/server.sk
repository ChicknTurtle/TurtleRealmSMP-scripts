
options:
    resourcePackUrl: https://chicknturtle.ninja/downloads/§aTurtleRealmSMP.zip
    resourcePackPrompt: "§bRequired for custom textures, sounds and other features"
    resourcePackDeclineMsg: "§cYou have declined the server resource pack."

# gamerules
on load:
    loop all worlds:
        set gamerule playersSleepingPercentage of loop-world to 0
        set gamerule doInsomnia of loop-world to false

function findResourcePackHash():
    log info "Fetching resource pack hash from {@resourcePackUrl}§r..."
    async run 0 ticks later:
        set {-resourcePackHash} to hashFromUrl("{@resourcePackUrl}")
        if {-resourcePackHash} isn't set:
            log error "Failed to fetch resource pack hash from {@resourcePackUrl}§r. Resource pack will not be sent to players."
        else:
            log info "Found resource pack hash: %{-resourcePackHash}%"
on skript load:
    findResourcePackHash()

# unlock recipes
# send resource pack
on join:
    sendResourcePack(player)
    unlock recipes (all recipes) for player

import:
    java.util.UUID
local function sendResourcePack(p:player):
    if {-resourcePackHash} is set:
        send resource pack "{@resourcePackUrl}" with id str(UUID.nameUUIDFromBytes("TurtleRealmSMP-main".getBytes())) with hash {-resourcePackHash} with prompt {@resourcePackPrompt} to {_p}

on resource pack decline:
    send {@resourcePackDeclineMsg} to player

every second:
    set {-formattedTps} to getFormattedTps()
    set {-formattedMspt} to getFormattedMspt()

function updateTablist(p:player):
    set {_players} to real online player count
    set {_tps} to ({-formattedTps} ? "§f. . .")
    set {_mspt} to ({-formattedMspt} ? "§f. . .")
    set {_servername} to ("§x§F§F§5§F§1§5&lTurtleRealmSMP Dev" if ({-env::dev} is true) else "§x§0§0§F§F§7§F§lTurtle§x§0§0§D§F§F§F§lRealm§x§2§d§b§4§4§1SMP")
    set tablist header of {_p} to "                                             %nl%%{_servername}%%nl%%nl%&7Players: %{_players}%%nl%"
    set tablist footer of {_p} to "%nl%&7TPS: %{_tps}% &8- &7MSPT: %{_mspt}%%nl%"

on server list ping:
    if {-env::dev} is true:
        set the motd to "§x§F§F§5§F§1§5▓▒░ §8─── §x§D§A§A§5§2§0« §x§0§0§F§F§7§F§lTurtle§x§0§0§D§F§F§F§lRealm §x§D§A§A§5§2§0» §8─── §x§F§F§5§F§1§5░▒▓§r%nl% §8────────── §x§F§F§5§F§1§5ᴅᴇᴠ §8──────────"
    else:
        set the motd to "§x§2§d§b§4§4§1▓▒░ §8─── §x§D§A§A§5§2§0« §x§0§0§F§F§7§F§lTurtle§x§0§0§D§F§F§F§lRealm §x§D§A§A§5§2§0» §8─── §x§2§d§b§4§4§1░▒▓§r%nl% §8────────── §x§2§d§b§4§4§1sᴍᴘ §8──────────"
