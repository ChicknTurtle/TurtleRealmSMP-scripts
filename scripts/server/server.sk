
import:
    org.geysermc.floodgate.api.FloodgateApi

options:
    resourcePackUrl: https://chicknturtle.ninja/downloads/§aTurtleRealmSMP.zip
    resourcePackPrompt: "§bRequired for custom textures, sounds and other features"
    resourcePackDeclineMsg: "§cYou have declined the server resource pack."

on load:
    set gamerule playersSleepingPercentage of (world "world") to 0
    set gamerule doInsomnia of (world "world") to false

    set {-fgApi} to FloodgateApi.getInstance()

function findResourcePackHash():
    log info "Fetching resource pack hash from {@resourcePackUrl}§r..."
    async run 0 ticks later:
        set {-resourcePackHash} to hashFromUrl("{@resourcePackUrl}")
        if {-resourcePackHash} isn't set:
            log error "Failed to fetch resource pack hash from {@resourcePackUrl}§r. Resource pack will not be sent to players."
        else:
            log info "Found resource pack hash: %{-resourcePackHash}%"
on skript load:
    findResourcePackHash()

# unlock recipes
# send resource pack
on join:
    unlock recipes (all recipes) for player
    (try {-fgApi}.isFloodgatePlayer(uuid of player)) isn't true
    sendResourcePack(player)

import:
    java.util.UUID
local function sendResourcePack(p:player):
    if {-resourcePackHash} is set:
        send resource pack "{@resourcePackUrl}" with id str(UUID.nameUUIDFromBytes("TurtleRealmSMP-main".getBytes())) with hash {-resourcePackHash} with prompt {@resourcePackPrompt} to {_p}

on resource pack decline:
    send {@resourcePackDeclineMsg} to player

every second:
    set {-formattedTps} to getFormattedTps()
    set {-formattedMspt} to getFormattedMspt()

on server list ping:
    if getConfigValue("root.server.devMode") is true:
        set the motd to "§x§F§F§5§F§1§5▓▒░ §8─── §x§D§A§A§5§2§0« §x§0§0§F§F§7§F§lTurtle§x§0§0§D§F§F§F§lRealm §x§D§A§A§5§2§0» §8─── §x§F§F§5§F§1§5░▒▓§r%nl% §8────────── §x§F§F§5§F§1§5ᴅᴇᴠ §8──────────"
    else:
        set the motd to "§x§2§d§b§4§4§1▓▒░ §8─── §x§D§A§A§5§2§0« §x§0§0§F§F§7§F§lTurtle§x§0§0§D§F§F§F§lRealm §x§D§A§A§5§2§0» §8─── §x§2§d§b§4§4§1░▒▓§r%nl% §8────────── §x§2§d§b§4§4§1sᴍᴘ §8──────────"
