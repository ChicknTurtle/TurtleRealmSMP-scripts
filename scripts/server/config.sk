
import:
    java.io.FileReader
    com.google.gson.Gson
    com.google.gson.JsonObject

on skript start:
    reloadServerConfig(true)

local function reloadServerConfig(initial:boolean=false):
    log info "%"L" if ({_initial} is true) else "Rel"%oading server config file..."
    set {_gson} to new Gson()
    set {-serverConfigJson} to {_gson}.fromJson(new FileReader("plugins/Skript/scripts/server/config.json"), JsonObject.class)

brig command tree /turtle:config:
    permission: op
    trigger:
        send "&cConfig doesn't have a gui right now. Use /config <path> [set <value>|reset]"
    literal arg "reload":
        trigger:
            send "Reloading server config..."
            reloadServerConfig()
    string arg "path":
        suggestions:
            apply suggestion getAllConfigValuePaths(true)
        trigger:
            set {_realPath} to "root.%{_path}%"
            set {_value} to getConfigValue({_realPath})
            if {_value} isn't set:
                send "§cConfig element '%{_path}%' doesn't exist."
                exit trigger
            send "§fConfig element §b%{_path}%§f is set to: %{_value}%"
        literal arg "set":
            greedy string arg "value":
                suggestions:
                    apply suggestions getConfigValueSuggestions("root.%{_path}%")
                trigger:
                    set {_realPath} to "root.%{_path}%"
                    set {_element} to getConfigElement({_realPath})
                    if {_element} isn't set:
                        send "§cConfig element '%{_path}%' doesn't exist."
                        exit trigger
                    set {_settingType} to try {_element}.get("type").getAsString()
                    if {_settingType} is "boolean":
                        {_value} isn't "true" or "false"
                        send "§cInvalid boolean value for element §b%{_path}%§c: %{_value}%"
                        exit trigger
                    else if {_settingType} is "slider":
                        set {_valueFloat} to ({_value} parsed as number)
                        if {_valueFloat} isn't set:
                            send "§cInvalid number value for element §b%{_path}%§c: %{_value}%"
                            exit trigger
                        set {_settingStart} to try {_element}.get("start").getAsFloat()
                        if {_settingStart} is set:
                            {_valueFloat} < {_settingStart}
                            send "§cValue is under min of %{_settingStart}% for element §b%{_path}%§c: %{_value}%"
                            exit trigger
                        set {_settingEnd} to try {_element}.get("end").getAsFloat()
                        if {_settingEnd} is set:
                            {_valueFloat} > {_settingEnd}
                            send "§cValue is over max of %{_settingEnd}% for element §b%{_path}%§c: %{_value}%"
                            exit trigger
                    else if {_settingType} is "text":
                        set {_settingMax} to try {_element}.get("max").getAsInt()
                        {_settingMax} is set
                        length of {_value} > {_settingMax}
                        send "§cValue is over max length of %{_settingMax}% for element §b%{_path}%§c: %{_value}%"
                        exit trigger
                    else if {_settingType} is "options":
                        set {_options::*} to ...(try {_element}.get("options").keySet())
                        {_options::*} doesn't contain {_value}
                        send "§cInvalid option for element §b%{_path}%§c: %{_value}%"
                        exit trigger
                    send "§fConfig element §b%{_path}%§f is set to: %{_value}%"
        literal arg "reset":
            trigger:
                set {_realPath} to "root.%{_path}%"
                set {_element} to getConfigElement({_realPath})
                if {_element} isn't set:
                    send "§cConfig element '%{_path}%' doesn't exist."
                    exit trigger
                delete {configValues::%{_path}%}
                send "§fConfig element §b%{_path}%§f has been reset to default."

function getConfigPaths(nodesJson:object, prefix:string, valuesOnly:boolean) :: strings:
    loop ...{_nodesJson}.keySet():
        if {_prefix} doesn't end with ".":
            {_prefix} isn't ""
            set {_prefix} to "%{_prefix}%."
        set {_fullPath} to "%{_prefix}%%loop-value%"
        set {_childJson} to {_nodesJson}.get(loop-value)
        set {_childNodes} to {_childJson}.get("nodes")
        set {_hasChildren} to (true if ({_childNodes} is set) else false)
        if any:
            {_valuesOnly} isn't true
            {_hasChildren} isn't true
        then:
            add {_fullPath} to {_paths::*}
        if {_hasChildren} is true:
            add getConfigPaths({_childNodes}, {_fullPath}, {_valuesOnly}) to {_paths::*}
    return {_paths::*}

function getAllConfigValuePaths(noRoot:boolean=false) :: strings:
    set {_prefix} to "" if {_noRoot} is true else "root"
    return getConfigPaths({-serverConfigJson}.get("root").get("nodes"), {_prefix}, true)

function getConfigValueSuggestions(path:string) :: strings:
    set {_element} to getConfigElement({_path})
    set {_settingType} to try {_element}.get("type").getAsString()
    if {_settingType} is "boolean":
        return "true", "false"
    else if {_settingType} is "slider":
        set {_settingStart} to try {_element}.get("start").getAsFloat()
        set {_settingDefault} to try {_element}.get("default").getAsFloat()
        set {_settingEnd} to try {_element}.get("end").getAsFloat()
        return {_settingStart}, {_settingDefault}, {_settingEnd}
    else if {_settingType} is "text":
        set {_settingDefault} to try {_element}.get("default").getAsString()
        return {_settingDefault}
    else if {_settingType} is "options":
        set {_options::*} to ...(try {_element}.get("options").keySet())
        return {_options::*}

function getConfigValue(path:string) :: object:
    if {configValues::%{_path}%} is set:
        return {configValues::%{_path}%}
    set {_element} to getConfigElement({_path})
    set {_settingType} to try {_element}.get("type").getAsString()
    set {_settingDefaultPrimitive} to try {_element}.get("default")
    if {_settingDefaultPrimitive} is set:
        if {_settingDefaultPrimitive}.isBoolean():
            return {_settingDefaultPrimitive}.getAsBoolean()
        else if {_settingDefaultPrimitive}.isNumber():
            return {_settingDefaultPrimitive}.getAsFloat()
        else:
            return {_settingDefaultPrimitive}.getAsString()
    else if {_settingType} is "slider":
        set {_settingStart} to try {_element}.get("start").getAsFloat()
        return {_settingStart}
    else if {_settingType} is "options":
        set {_options::*} to ...(try {_element}.get("options").keySet())
        return first element of {_options::*}

function getConfigElement(path:string) :: object:
    set {_path::*} to ({_path} split at ".")
    set {_pageJson} to {-serverConfigJson}
    loop {_path::*}:
        set {_pageJson} to {_pageJson}.get(loop-value)
        loop-iteration isn't size of {_path::*}
        set {_pageJson} to {_pageJson}.get("nodes")
    return {_pageJson}
