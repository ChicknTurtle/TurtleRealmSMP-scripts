
import:
    net.minecraft.core.BlockPos

# Send game events to players
# https://minecraft.wiki/w/Minecraft_Wiki:Projects/wiki.vg_merge/Protocol#World_Event
effect:
    patterns:
        (send|play) (game|level)[ ]event %integer% at %block% [with [[extra ]data] %-number%]
    trigger:
        set {_event} to expr-1
        set {_block} to expr-2
        set {_data} to expr-3 ? 0
        (world of {_block}).getHandle().levelEvent({_event}, new BlockPos(floor(x-coord of {_block}),floor(y-coord of {_block}),floor(z-coord of {_block})), {_data})

# Use this for entity statuses:
# https://skripthub.net/docs/?id=10516

function getBlockGameEventDir(facing:direction) :: integer:
    if {_facing} is below:
        return 0
    else if {_facing} is above:
        return 1
    else if {_facing} is north:
        return 2
    else if {_facing} is south:
        return 3
    else if {_facing} is west:
        return 4
    else if {_facing} is east:
        return 5

function dispenserEvent(block:block):
    {_block} is a dispenser
    set {_data} to getBlockGameEventDir(facing of {_block})
    play game event 1000 at {_block} with data {_data}
    play game event 2000 at {_block} with data {_data}

function dispenserEventFail(block:block):
    {_block} is a dispenser
    set {_data} to getBlockGameEventDir(facing of {_block})
    play game event 1001 at {_block} with data {_data}
    play game event 2000 at {_block} with data {_data}
